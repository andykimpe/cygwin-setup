dnl Process this file with autoconf to produce a configure script. 
AC_REVISION($Id$)
# -*- shell-script -*-

# Copyright (C) 1999, 2000, 2001 by Martin Pool <mbp@samba.org>

                    # Linux is the gateway drug for freedom.
                    #                -- Don Marti

my_url=http://rproxy.samba.org/
echo "Configuring $my_package $my_version   ($my_url)"

AC_PREREQ(2.53)
AC_INIT(librsync, 0.9.3)
AC_CONFIG_SRCDIR([trace.c])

AM_INIT_AUTOMAKE

AM_DISABLE_SHARED
AC_CANONICAL_HOST
AC_PROG_LIBTOOL

AM_CONFIG_HEADER(config.h)

# Set up to get as many features as we can manage
AC_DEFINE(_GNU_SOURCE,,[include GNU C library extensions.])

dnl Checks for programs.
AC_PROG_CC
AC_ISC_POSIX
AC_PROG_CPP
AC_PROG_INSTALL

AC_TYPE_SIZE_T
AC_TYPE_OFF_T

AC_C_CONST

AC_SYS_LARGEFILE

# See if the compiler has the nice GNU feature of 
# putting argv[0] into a global
AC_MSG_CHECKING([for program_invocation_short_name])
AC_TRY_COMPILE([#include <errno.h>], 
    [strlen(program_invocation_short_name)],
    [ AC_DEFINE(HAVE_PROGRAM_INVOCATION_NAME,,[GNU extension of saving argv[0] to program_invocation_short_name])
      AC_MSG_RESULT(yes) ],
    [ AC_MSG_RESULT(no) ])

# supply a snprintf if the system doesn't have one
AC_REPLACE_FUNCS(snprintf)
AC_CHECK_FUNCS(vsnprintf)
AC_CHECK_FUNCS(_vsnprintf)
AC_CHECK_FUNCS(strerror mtrace)
AC_CHECK_FUNC(setreuid, [], [
    AC_CHECK_LIB(ucb, setreuid, [if echo $LIBS | grep -- -lucb >/dev/null ;then :; else LIBS="$LIBS -lc -lucb" USEUCB=y;fi])
])

AC_MSG_CHECKING([whether to build rdiff])
AC_ARG_ENABLE(rdiff,
	      AC_HELP_STRING([--enable-rdiff],
			     [Build the rdiff tool]),
	      ac_cv_enable_rdiff=$enableval, ac_cv_enable_rdiff=no)
AC_MSG_RESULT([$ac_cv_enable_rdiff])

AC_MSG_CHECKING(whether to use included libpopt)
AC_ARG_WITH(included-popt,
	    AC_HELP_STRING([--with-included-popt],
      			   [use bundled popt library for rdfiff, not fromsystem]))
if test x"$ac_cv_enable_rdiff" = x"yes"; then
  AM_CONDITIONAL(BUILDRDIFF, true)
  if test x"$with_included_popt" != x"yes"
  then
    AC_CHECK_LIB(popt, poptGetContext, , [with_included_popt=yes])
  fi
  if test x"$with_included_popt" = x"yes"
  then
  AC_MSG_RESULT("$srcdir/popt")
  BUILD_POPT='popt/libpopt.a'
  CFLAGS="$CFLAGS -I$srcdir/popt"
  EXTRA_SUBDIRS=popt
  else
  AC_MSG_RESULT(no)
  fi
else
  AM_CONDITIONAL(BUILDRDIFF, false)
  AC_MSG_RESULT(disabled without rdiff)
fi

dnl Checks for declarations
AC_MSG_CHECKING(for sockaddr.sin_len)
AC_EGREP_HEADER(sin_len, sys/socket.h, 
	[ AC_DEFINE(HAVE_SOCK_SIN_LEN,,[Define this if your sockaddr structure contains sin_len])
          AC_MSG_RESULT(yes) ],
        [ AC_MSG_RESULT(no) ])

AC_MSG_CHECKING(for intmax_t in stdint.h)
AC_EGREP_HEADER(intmax_t, stdint.h,
        [ AC_MSG_RESULT(yes) ],
        [ AC_DEFINE(intmax_t, long,[Define to a replacement type if intmax_t is not a builtin, or in sys/types.h or stdlib.h or stddef.h])
          AC_MSG_RESULT(no) ])

dnl Checks for libraries

AC_CHECK_LIB(z, deflate)
AC_CHECK_LIB(bz2, BZ2_bzCompress)

dnl Check configuration options
if test "$GCC" = "yes" 
then 
    # I'm fussy.
    CFLAGS="-Wall -Wshadow -Wundef -Wwrite-strings -Wmissing-prototypes -Wmissing-declarations -Wstrict-prototypes -Wpointer-arith -Wcast-qual -Wcast-align $CFLAGS"
fi
# TODO: Similar conditions for other known compilers.  For SUNWspro, use `-v'.

AC_ARG_ENABLE(ccmalloc,
	[  --enable-ccmalloc       use ccmalloc debugger (default no)],
	[ LIBS="$LIBS -lccmalloc -ldl" ])

AC_ARG_ENABLE(trace,
        [  --disable-trace         turn off library tracing],,enable_trace=no)
AC_MSG_CHECKING(whether to include trace code)
if test "$enable_trace" = "yes" 
then
    AC_MSG_RESULT(yes)
    AC_DEFINE(DO_RS_TRACE,,[Define this to enable trace code])
elif test "$enable_trace" = "no"
then
    AC_MSG_RESULT(no)
else
    AC_MSG_RESULT(failed)
    AC_MSG_ERROR("--enable-trace must be set to yes or no")
fi

AC_CHECK_SIZEOF([unsigned char], 1)
if test "$ac_cv_sizeof_unsigned_char" -ne 1
then
    AC_MSG_WARN(unsigned char seems to be $ac_cv_sizeof_unsigned_char bytes.  Expect trouble.)
fi

AC_CHECK_SIZEOF(short, 2)
AC_CHECK_SIZEOF(unsigned short, 2)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(unsigned int, 4)
AC_CHECK_SIZEOF(long, 4)
AC_CHECK_SIZEOF(unsigned long, 4)
AC_CHECK_SIZEOF(long long, 0)
AC_CHECK_SIZEOF(off_t, 4)
AC_CHECK_SIZEOF(size_t, 4)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(config.h unistd.h alloca.h libintl.h mcheck.h)
AC_CHECK_HEADERS(zlib.h bzlib.h stdint.h)

# GNU library versioning: This is NOT the librsync release number.
# Rather, it is a CURRENT:REVISION:AGE tuple describing binary
# compatibility to libtool.  It is only updated on major public
# releases where there is the possibility of binaries hanging around.

librsync_libversion=2:0:0
AC_DEFINE_UNQUOTED(RS_LIBVERSION, "$librsync_libversion",[Version of the libtool interface.])

AC_DEFINE_UNQUOTED(RS_CANONICAL_HOST, "$host",[Canonical GNU hostname])

AC_SUBST(librsync_libversion)
AC_SUBST(OBJ_SAVE)
AC_SUBST(OBJ_RESTORE)
AC_SUBST(CC_SHOBJ_FLAG)
AC_SUBST(CCMALLOC_LIB)
AC_SUBST(net_libs)
AC_SUBST(LIBTOOL_DEPS)
AC_SUBST(BUILD_POPT)
AC_SUBST(EXTRA_SUBDIRS)

AC_CONFIG_FILES([Makefile popt/Makefile])
AC_OUTPUT
